prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix ltpch: <http://extbi.lab.aau.dk/ontology/ltpch/>
prefix fn: <http://www.w3.org/2005/xpath-functions#>

select
  ?p_brand
  ?p_type
  ?p_size
  (count(distinct ?supp) as ?supplier_cnt)
where {
    ?ps a ltpch:partsupp ;
        ltpch:ps_has_part ?part ;
        ltpch:ps_has_supplier ?supp .
    ?part ltpch:p_brand ?p_brand ;
        ltpch:p_type ?p_type ;
        ltpch:p_size ?p_size .    
    filter (
      (?p_brand != "BRAND#24") &&
      !(fn:starts-with(?p_type,"MEDIUM BRUSHED")) &&
      (xsd:integer(?p_size) in (38, 19, 7, 30, 6, 1, 40, 22))
    )
    filter NOT EXISTS {
       ?supp a ltpch:supplier;
             ltpch:s_comment ?badcomment .
       filter ( fn:matches (?badcomment ,"Customer.*Complaints") )
    }
  }
group by
  ?p_brand
  ?p_type
  ?p_size
order by
  desc ((count(distinct ?supp)))
  ?p_brand
  ?p_type
  ?p_size
