prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix fn: <http://www.w3.org/2005/xpath-functions#>

select
  ?p_brand
  ?p_type
  ?p_size
  (count(distinct ?supp) as ?supplier_cnt)
where {
    ?ps a rdfh:partsupp ;
        rdfh:ps_has_part ?part ;
        rdfh:ps_has_supplier ?supp .
    ?part rdfh:p_brand ?p_brand ;
        rdfh:p_type ?p_type ;
        rdfh:p_size ?p_size .    
    filter (
      (?p_brand != "BRAND#24") &&
      !(fn:starts-with(?p_type,"MEDIUM BRUSHED")) &&
      (xsd:integer(?p_size) in (38, 19, 7, 30, 6, 1, 40, 22))
    )
    filter NOT EXISTS {
       ?supp a rdfh:supplier;
             rdfh:s_comment ?badcomment .
       filter ( fn:matches (?badcomment ,"Customer.*Complaints") )
    }
  }
group by
  ?p_brand
  ?p_type
  ?p_size
order by
  desc ((count(distinct ?supp)))
  ?p_brand
  ?p_type
  ?p_size