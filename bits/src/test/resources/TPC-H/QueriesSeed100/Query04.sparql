prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix qb: <http://purl.org/linked-data/cube#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 

select
  ?o_orderpriority
  (count(*) as ?order_count)
where  
{
  {
    select distinct
      ?o_orderpriority
      ?ord
    where 
    {
      ?li 
          rdfh:l_has_order ?ord ;
          rdfh:l_commitdate ?l_commitdate ;
          rdfh:l_receiptdate ?l_receiptdate .
      ?ord rdfh:o_orderpriority ?o_orderpriority ;
           rdfh:o_orderdate ?o_orderdate .
      filter (
        (?l_commitdate < ?l_receiptdate) &&
        (?o_orderdate >= "1995-10-01"^^xsd:date) &&
        (?o_orderdate < ("1995-10-01"^^xsd:date + "P3M"^^xsd:duration))
      )
    }
  }
}
group by
  ?o_orderpriority
order by
  ?o_orderpriority
