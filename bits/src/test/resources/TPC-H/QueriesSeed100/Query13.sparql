prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?c_count
  (count(1) as ?custdist)
where {
    { select
        ?c_custkey
        (count (?ord) as ?c_count)
      where
        {
          ?cust rdfh:c_custkey ?c_custkey .
           optional {
             ?ord a rdfh:orders ;
                  rdfh:has_customer ?cust ;
                  rdfh:o_comment ?o_comment .
              filter (!( REGEX (?o_comment , "pending.*deposits" ) ) ) . 
          }
        }
      group by 
        ?c_custkey
    }
  }
group by
  ?c_count
order by
  desc (count(1))
  desc (?c_count)
