prefix xsd: <http://www.w3.org/2001/XMLSchema#>  
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?s_name
  ?s_address
where
{
  ?supp rdfh:s_name ?s_name ;
        rdfh:s_address ?s_address .
  { 
    select distinct 
      ?supp 
    where 
    {
      ?big_ps a rdfh:partsupplier ;
      			rdfh:has_part ?part ;
              rdfh:ps_availqty ?big_ps_availqty ;
              rdfh:has_supplier ?supp .
      ?supp rdfh:has_nation ?s_nation .
      ?s_nation rdfh:n_name ?n_name .
      ?part rdfh:p_name ?p_name . 
      filter (REGEX (?p_name , "^brown") && 
              ?n_name = "EGYPT" && 
              (?big_ps_availqty) > ?qty_threshold)
      {
        select 
          ((0.5 * sum((?l_linequantity))) as ?qty_threshold)
          ?part ?supp
        where
        {
          ?li a rdfh:lineitem ;
              rdfh:l_shipdate ?l_shipdate ;
              rdfh:l_linequantity ?l_linequantity ;
              rdfh:has_part ?part ;
              rdfh:has_supplier ?supp .
          filter ((?l_shipdate >= "1997-01-01"^^xsd:date) &&
            (?l_shipdate < ("1997-01-01"^^xsd:date + "P1Y"^^xsd:duration))
          )
        }
        group by 
          ?part ?supp
      }
    } 
  }
}
order by ?s_name
