prefix xsd: <http://www.w3.org/2001/XMLSchema#>  
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?s_name
  ?s_address
where
{
  ?supp rdfh:s_name ?s_name ;
        rdfh:s_address ?s_address .
  { 
    select distinct 
      ?supp 
    where 
    {
      ?big_ps rdfh:ps_has_part ?part ;
              rdfh:ps_availqty ?big_ps_availqty ;
              rdfh:ps_has_supplier ?supp .
      ?supp rdfh:s_has_nation ?s_nation .
      ?s_nation rdfh:n_name ?n_name .
      ?part rdfh:p_name ?p_name . 
      filter (REGEX (?p_name , "^brown") && 
              ?n_name = "EGYPT" && 
              xsd:decimal(?big_ps_availqty) > ?qty_threshold)
      {
        select 
          ((0.5 * sum(xsd:decimal(?l_linequantity))) as ?qty_threshold)
          ?big_ps
        where
        {
          ?li qb:dataSet rdfh:lineitemCube ;
              rdfh:l_shipdate ?l_shipdate ;
              rdfh:l_linequantity ?l_linequantity ;
              rdfh:l_has_partsupplier ?big_ps .
          filter ((xsd:date(?l_shipdate) >= xsd:date("1997-01-01"^^xsd:date)) &&
            (xsd:date(?l_shipdate) < xsd:date("1997-01-01"^^xsd:date + "P1Y"^^xsd:duration))
          )
        }
        group by 
          ?big_ps
      }
    } 
  }
}
order by ?s_name
