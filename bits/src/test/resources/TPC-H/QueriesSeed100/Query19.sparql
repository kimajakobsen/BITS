prefix xsd: <http://www.w3.org/2001/XMLSchema#>  
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix qb: <http://purl.org/linked-data/cube#>


select
  ((sum(xsd:decimal(?l_lineextendedprice) * (1 - xsd:decimal(?l_linediscount)))) as ?revenue)
where {
    ?li qb:dataSet rdfh:lineitemCube ;
        rdfh:l_lineextendedprice ?l_lineextendedprice ;
        rdfh:l_linediscount ?l_linediscount ;
        rdfh:l_linequantity ?l_linequantity ;
        rdfh:l_shipmode ?l_shipmode ;
        rdfh:l_shipinstruct ?l_shipinstruct ;
        rdfh:l_has_partsupplier ?ps .
    ?ps rdfh:ps_has_part ?part .
     ?part rdfh:p_brand ?p_brand ;
          rdfh:p_size ?p_size ;
          rdfh:p_container ?p_container .
     
     filter (?l_shipmode in ("AIR", "AIR REG") &&
      ?l_shipinstruct = "DELIVER IN PERSON" &&
      ( ( (REGEX(?p_brand,"^BRAND#54$","i")) &&
          (?p_container in ("SM CASE", "SM BOX", "SM PACK", "SM PKG")) &&
          (xsd:integer(?l_linequantity) >= 1) &&
          (xsd:integer(?l_linequantity) <= (1 + 10)) &&
          (xsd:integer(?p_size) >= 1) && (xsd:integer(?p_size) <= 5) ) ||
        ( (REGEX(?p_brand,"^BRAND#51$","i")) &&
          (?p_container in ("MED BAG", "MED BOX", "MED PKG", "MED PACK")) &&
          (xsd:integer(?l_linequantity) >= 16) && 
          (xsd:integer(?l_linequantity) <= (16 + 10)) &&
          (xsd:integer(?p_size) >= 1) && (xsd:integer(?p_size) <= 10) ) ||
        ( (REGEX(?p_brand,"^BRAND#13$","i")) &&
          (?p_container in ("LG CASE", "LG BOX", "LG PACK", "LG PKG")) &&
          (xsd:integer(?l_linequantity) >= 28) &&
          (xsd:integer(?l_linequantity) <= (28 + 10)) &&
          (xsd:integer(?p_size) >= 1) && (xsd:integer(?p_size) <= 15) ) ) )
  }
