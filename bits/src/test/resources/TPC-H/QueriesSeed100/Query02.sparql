prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix fn: <http://www.w3.org/2005/xpath-functions#>

select
  ?s_acctbal
  ?s_name
  ?nation_name
  ?p_partkey
  ?p_mfgr
  ?s_address
  ?s_phone
  ?s_comment
where {
  ?ps a rdfh:partsupp;
      rdfh:ps_has_supplier ?supp;
      rdfh:ps_has_part ?part ;
      rdfh:ps_supplycost ?minsc .
  ?supp a rdfh:supplier ;
     rdfh:s_acctbal ?s_acctbal ;
         rdfh:s_name ?s_name ;
     rdfh:s_has_nation ?s_has_nation ;
     rdfh:s_address ?s_address ;
     rdfh:s_phone ?s_phone ;
     rdfh:s_comment ?s_comment .
  ?s_has_nation rdfh:n_name ?nation_name ;
     rdfh:n_has_region ?s_has_region .
  ?s_has_region rdfh:r_name "EUROPE" .
  ?part a rdfh:part ;
      rdfh:p_partkey ?p_partkey ;
      rdfh:p_mfgr ?p_mfgr ;
      rdfh:p_size ?size ;
      rdfh:p_type ?p_type .
  FILTER (?size = "1" && fn:contains(?p_type, "COPPER")) .
  { select ?part (min(?s_cost) as ?minsc)
    where {
        ?ps a rdfh:partsupp;
            rdfh:ps_has_part ?part;
            rdfh:ps_has_supplier ?ms;
            rdfh:ps_supplycost ?s_cost .
        ?ms rdfh:s_has_nation ?m_has_nation .
        ?m_has_nation rdfh:n_has_region ?m_has_region .
        ?m_has_region rdfh:r_name "EUROPE" .
      } 
      group by ?part 
    }
  }
order by
  desc (?s_acctbal)
  ?nation_name
  ?s_name
  ?p_partkey
limit 100