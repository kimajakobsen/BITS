prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 

select
  ?bigpspart
  ?bigpsvalue
where {
      { select
          ?bigpspart
          (sum((?b_supplycost) * (?b_availqty)) as ?bigpsvalue)
        where
          {
            ?bigps a rdfh:partsupp ;
                   rdfh:has_part ?bigpspart ;
                   rdfh:ps_supplycost ?b_supplycost ;
                   rdfh:ps_availqty ?b_availqty ;
                   rdfh:has_supplier ?b_supplier .
            ?b_supplier rdfh:has_nation ?b_nation .
            ?b_nation rdfh:n_name "BRAZIL" .
          }
          group by ?bigpspart
      }
      {
       select
          (sum((?t_supplycost) * (?t_availqty)) * 0.000100 as ?threshold)
        where
          {
            ?thr_ps a rdfh:partsupp ;
                    rdfh:ps_supplycost ?t_supplycost ;
                    rdfh:ps_availqty ?t_availqty ;
                    rdfh:has_supplier ?t_supplier .
            ?t_supplier rdfh:has_nation ?t_nation .
            ?t_nation rdfh:n_name "BRAZIL" .
          }
      }
      filter (?bigpsvalue > ?threshold )
  }
order by
  desc (?bigpsvalue)
