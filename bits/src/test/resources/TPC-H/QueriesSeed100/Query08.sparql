prefix afn: <http://jena.hpl.hp.com/ARQ/function#>
prefix fn: <http://www.w3.org/2005/xpath-functions#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?o_year
  ((?sum1 / ?sum2) as ?mkt_share)
where {
  { select
    ?o_year
    (sum (?volume * xsd:integer(fn:starts-with(?nation, "ETHIOPIA"))) as ?sum1)
    (sum (?volume) as ?sum2)
    where {
      { select
           ((YEAR (xsd:date(?o_orderdate))) as ?o_year)
           (((?l_lineextendedprice) * (1 - (?l_linediscount))) as ?volume)
           ?nation
         where {
           ?li rdfh:has_order ?ord ;
               rdfh:has_part ?part ;
               rdfh:has_supplier ?s_supplier ;
               rdfh:l_lineextendedprice ?l_lineextendedprice ;
               rdfh:l_linediscount ?l_linediscount .
           ?s_supplier rdfh:has_nation ?n2 .
           ?n2 rdfh:n_name ?nation .
           ?part rdfh:p_type ?type .
           ?ord rdfh:o_orderdate ?o_orderdate ;
             rdfh:has_customer ?c_customer .
           ?c_customer rdfh:has_nation ?n_nation .
           ?n_nation rdfh:has_region ?r_region .
           ?r_region rdfh:r_name ?region.
           filter ((?o_orderdate >= "1995-01-01"^^xsd:date) &&
             (?o_orderdate <= "1996-12-31"^^xsd:date) &&
              (?region = "AFRICA") &&
              (?type = "STANDARD POLISHED COPPER") 
           ) 
        } 
      } 
    }
    group by
      ?o_year 
  } 
}
order by
  ?o_year
