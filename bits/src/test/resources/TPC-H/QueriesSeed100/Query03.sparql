prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix qb: <http://purl.org/linked-data/cube#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

select
  ?o_orderkey
  (sum((?l_lineextendedprice)*(1 - (?l_linediscount))) as ?revenue)
  ?o_orderdate
  ?o_shippriority
where  {
  ?li 
          rdfh:l_lineextendedprice ?l_lineextendedprice ;
    rdfh:l_linediscount ?l_linediscount ;
    rdfh:has_order ?ord ;
    rdfh:l_shipdate ?l_shipdate .
  ?ord rdfh:o_orderdate ?o_orderdate ;
    rdfh:o_shippriority ?o_shippriority ;
    rdfh:o_orderkey ?o_orderkey ;
    rdfh:has_customer ?cust .
  ?cust rdfh:c_mktsegment ?c_mktsegment .
  filter ((xsd:date(?o_orderdate) < "1995-03-30"^^xsd:date) &&
    (xsd:date(?l_shipdate) > "1995-03-30"^^xsd:date) &&
    (?c_mktsegment = "FURNITURE") ) 
}
group by
  ?o_orderkey
  ?o_orderdate
  ?o_shippriority
order by
  desc (sum ((?l_lineextendedprice) * (1 - (?l_linediscount))))
  ?o_orderdate
limit 10
