prefix xsd: <http://www.w3.org/2001/XMLSchema#>  
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix qb: <http://purl.org/linked-data/cube#>
prefix fn: <http://www.w3.org/2005/xpath-functions#>

select
  ?l_shipmode
  (sum (
      xsd:integer(fn:starts-with(?o_orderpriority, "1-URGENT") ||
      fn:starts-with(?o_orderpriority, "2-HIGH") ) ) as ?high_line_count)
  (sum (1 -
      xsd:integer(fn:starts-with(?o_orderpriority, "1-URGENT") ||
      fn:starts-with(?o_orderpriority, "2-HIGH") ) ) as ?low_line_count)
where  {
    
    ?li qb:dataSet rdfh:lineitemCube ;
       rdfh:l_has_order ?ord ;
       rdfh:l_commitdate ?l_commitdate ;
       rdfh:l_receiptdate ?l_receiptdate ;
       rdfh:l_shipmode ?l_shipmode ;
       rdfh:l_shipdate ?l_shipdate .
    ?ord rdfh:o_orderpriority ?o_orderpriority .
    filter (?l_shipmode in ("REG AIR", "AIR") &&
      (xsd:date(?l_commitdate) < xsd:date(?l_receiptdate)) &&
      (xsd:date(?l_shipdate) < xsd:date(?l_commitdate)) &&
      (xsd:date(?l_receiptdate) >= "1996-01-01"^^xsd:date) &&
      (xsd:date(?l_receiptdate) < ("1996-01-01"^^xsd:date + "P1Y"^^xsd:duration)) )
  }
group by
  ?l_shipmode
order by
  ?l_shipmode
