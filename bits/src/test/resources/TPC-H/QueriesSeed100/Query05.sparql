prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?nation
  (sum((?l_lineextendedprice) * (1 - (?l_linediscount))) as ?revenue)
where  {
   ?li 
       rdfh:has_order ?ord ;
       rdfh:has_supplier ?supp ;
       rdfh:l_lineextendedprice ?l_lineextendedprice ;
       rdfh:l_linediscount ?l_linediscount .
    ?ord rdfh:has_customer ?cust ;
         rdfh:o_orderdate ?o_orderdate .
    ?supp rdfh:has_nation ?s_nation .
    ?s_nation rdfh:has_region ?s_region ;
              rdfh:n_name ?nation .
    ?s_region rdfh:r_name ?r_name .
    ?cust rdfh:has_nation ?c_nation.
    filter ((?c_nation = ?s_nation) &&
      (xsd:date(?o_orderdate) >= "1996-01-01"^^xsd:date) &&
      (xsd:date(?o_orderdate) < ("1996-01-01"^^xsd:date + "P1Y"^^xsd:duration)) &&
      (?r_name = "AFRICA") ) 
  }
group by
  ?nation
order by
  desc (sum((?l_lineextendedprice) * (1 - (?l_linediscount))))
