prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?nation
  (sum(xsd:decimal(?l_lineextendedprice) * (1 - xsd:decimal(?l_linediscount))) as ?revenue)
where  {
   ?li qb:dataSet rdfh:lineitemCube ;
       rdfh:l_has_order ?ord ;
       rdfh:l_has_partsupplier ?ps ;
       rdfh:l_lineextendedprice ?l_lineextendedprice ;
       rdfh:l_linediscount ?l_linediscount .
    ?ord rdfh:o_has_customer ?cust ;
         rdfh:o_orderdate ?o_orderdate .
    ?ps rdfh:ps_has_supplier ?supp .
          ?supp rdfh:s_has_nation ?s_nation .
    ?s_nation rdfh:n_has_region ?s_region ;
              rdfh:n_name ?nation .
    ?s_region rdfh:r_name ?r_name .
    ?cust rdfh:c_has_nation ?c_nation.
    filter ((?c_nation = ?s_nation) &&
      (xsd:date(?o_orderdate) >= "1996-01-01"^^xsd:date) &&
      (xsd:date(?o_orderdate) < ("1996-01-01"^^xsd:date + "P1Y"^^xsd:duration)) &&
      (?r_name = "AFRICA") ) 
  }
group by
  ?nation
order by
  desc (sum(xsd:decimal(?l_lineextendedprice) * (1 - xsd:decimal(?l_linediscount))))
