prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix qb: <http://purl.org/linked-data/cube#>

select 
  ?supp_nation 
  ?cust_nation 
  ?li_year
  (sum (?volume) as ?revenue)
where {
  {
    select
      ?supp_nation
      ?cust_nation
      ?li_year
      (((?l_lineextendedprice) * (1 - (?l_linediscount))) as ?volume)
    where {
      ?li 
        rdfh:has_order ?ord ;
        rdfh:has_partsupplier ?ps ;
        rdfh:l_shipdate ?l_shipdate ;
        rdfh:l_lineextendedprice ?l_lineextendedprice ;
        rdfh:l_linediscount ?l_linediscount .
      ?ord rdfh:has_customer ?cust .
      ?cust rdfh:has_nation ?custn .
      ?custn rdfh:n_name ?cust_nation .
      ?ps rdfh:has_supplier ?supp .
      ?supp rdfh:has_nation ?suppn .
      ?suppn rdfh:n_name ?supp_nation .
      BIND (SUBSTR(STR(?l_shipdate), 1,4) as ?li_year)
      filter (xsd:boolean(
        (?cust_nation = "VIETNAM" && ?supp_nation = "INDONESIA") ||
        (?cust_nation = "INDONESIA" && ?supp_nation = "VIETNAM") ) &&
        xsd:boolean(xsd:date(?l_shipdate) >= xsd:date("1995-01-01"^^xsd:date)) &&
        xsd:boolean(xsd:date(?l_shipdate) <= xsd:date("1996-12-31"^^xsd:date)) ) 
      } 
   } 
}
group by
  ?supp_nation
  ?cust_nation
  ?li_year
order by
  ?supp_nation
  ?cust_nation
  ?li_year
