prefix xsd: <http://www.w3.org/2001/XMLSchema#>  
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
    ?s_name
    (count(1) as ?numwait)
where {
          ?li1 
              rdfh:l_receiptdate ?l1_receiptdate ;
              rdfh:l_commitdate ?l1_commitdate ;
              rdfh:l_has_partsupplier ?ps ;
              rdfh:l_has_order ?ord .
          ?ps rdfh:ps_has_supplier ?supp .
          ?supp rdfh:s_name ?s_name ;
               rdfh:s_has_nation ?s_nation .
          ?ord rdfh:o_orderstatus ?orderstatus .
          ?s_nation rdfh:n_name ?name
          filter ( 
            xsd:date(?l1_receiptdate) > xsd:date(?l1_commitdate) && 
            ?name = "CANADA" && 
            ?orderstatus = "F"
            ) 
          filter exists {
            ?li2 rdfh:l_has_order ?ord ;
                 rdfh:l_has_partsupplier ?ps2 .
            ?ps2 rdfh:ps_has_supplier ?supp2 .
            filter (?supp != ?supp2)
          }
          filter not exists {
              ?li3 rdfh:l_has_order ?ord ;
                   rdfh:l_receiptdate ?l3_receiptdate ;
                   rdfh:l_commitdate ?l3_commitdate ;
                   rdfh:l_has_partsupplier ?ps3 .
              ?ps3 rdfh:ps_has_supplier ?supp3 .
              filter (
                 xsd:date(?l3_receiptdate) > xsd:date(?l3_commitdate) &&
                 ?supp3 != ?supp
              )
         }
       }
group by
   ?s_name
order by
    desc (count(1))
    ?s_name