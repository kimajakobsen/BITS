prefix xsd: <http://www.w3.org/2001/XMLSchema#>  
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?s_suppkey
  ?s_name
  ?s_address
  ?s_phone
  ?total_revenue
where  {
    ?supplier a rdfh:supplier ;
        rdfh:s_suppkey ?s_suppkey ;
        rdfh:s_name ?s_name ;
        rdfh:s_address ?s_address ;
        rdfh:s_phone ?s_phone .
    { select
          ?supplier
          (sum(xsd:decimal(?l_extendedprice) * (1 - xsd:decimal(?l_discount))) as ?total_revenue)
       where {
            ?li1 qb:dataSet rdfh:lineitemCube ;
                 rdfh:l_shipdate ?l_shipdate ;
                 rdfh:l_lineextendedprice ?l_extendedprice ;
                 rdfh:l_linediscount ?l_discount ;
                 rdfh:l_has_partsupplier ?ps1 .
            ?ps1 rdfh:ps_has_supplier ?supplier .
            filter (
                xsd:date(?l_shipdate) >= xsd:date("1994-04-01"^^xsd:date) &&
                xsd:date(?l_shipdate) < xsd:date("1994-04-01"^^xsd:date + "P3M"^^xsd:duration) )
        }
      group by
        ?supplier
      }
      { select (max (?l2_total_revenue) as ?maxtotal)
        where {
            { select
                  ?supplier2
                  (sum(xsd:decimal(?l2_extendedprice) * (1 - xsd:decimal(?l2_discount))) as ?l2_total_revenue)
               where {
                    ?li2 qb:dataSet rdfh:lineitemCube ;
                      rdfh:l_shipdate ?l2_shipdate ;
                      rdfh:l_lineextendedprice ?l2_extendedprice ;
                      rdfh:l_linediscount ?l2_discount ;
                       rdfh:l_has_partsupplier ?ps2 .
                  ?ps2 rdfh:ps_has_supplier ?supplier2 .
                    filter (
                        xsd:date(?l2_shipdate) >= xsd:date("1994-04-01"^^xsd:date) &&
                        xsd:date(?l2_shipdate) < xsd:date("1994-04-01"^^xsd:date + "P3M"^^xsd:duration) )
               }
               group by 
                ?supplier2
            }
        }
    }
    filter (?total_revenue = ?maxtotal)
}
order by
  ?supplier
