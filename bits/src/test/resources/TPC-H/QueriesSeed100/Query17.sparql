prefix xsd: <http://www.w3.org/2001/XMLSchema#>  
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ((sum((?l_lineextendedprice)) / 7.0) as ?avg_yearly)
where {
    ?li 
        rdfh:l_lineextendedprice ?l_lineextendedprice ;
        rdfh:l_linequantity ?l_linequantity ;
        rdfh:l_has_partsupplier ?ps .
    ?ps rdfh:ps_has_part ?part .
    ?part rdfh:p_brand ?p_brand ;
          rdfh:p_container ?p_container .
          {
            select 
              ?part
              ((0.2 * avg((?l2_linequantity))) as ?threshold)
            where { 
              ?li2  a rdfh:lineitem ;
                    rdfh:l_linequantity ?l2_linequantity ; 
                    rdfh:l_has_partsupplier ?ps2 .
              ?ps2  rdfh:ps_has_part ?part .
          } 
          group by
            ?part
        }
    filter ((?l_linequantity) < ?threshold && REGEX(?p_brand,"BRAND#32","i") && ?p_container = "MED JAR") 
}
