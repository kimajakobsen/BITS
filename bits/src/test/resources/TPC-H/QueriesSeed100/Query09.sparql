prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix rdfh-inst: <http://lod2.eu/schemas/rdfh-inst#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
  ?nation
  ?o_year
  (sum(?amount) as ?sum_profit)
where {
  { select
      ?nation
      ?o_year
      (((?l_lineextendedprice) * (1 - (?l_linediscount)) - (?ps_supplycost) * (?l_linequantity)) as ?amount)
    where {
      ?li 
          rdfh:has_order ?ord ;
          rdfh:has_partsupplier ?ps ;
          rdfh:l_linequantity ?l_linequantity ;
          rdfh:l_lineextendedprice ?l_lineextendedprice ;
          rdfh:l_linediscount ?l_linediscount .
      ?ps rdfh:has_part ?part ;
          rdfh:has_supplier ?supp .
      ?supp rdfh:has_nation ?s_nation .
      ?s_nation rdfh:n_name ?nation .
      ?ord rdfh:o_orderdate ?o_orderdate .
      ?ps rdfh:ps_supplycost ?ps_supplycost .
      ?part rdfh:p_name ?p_name .
      filter (REGEX (?p_name, "khaki"))
      BIND (SUBSTR(STR(?o_orderdate), 1,4) as ?o_year)
    } 
  } 
}
group by
  ?nation
  ?o_year
order by
  ?nation
  desc (?o_year)
