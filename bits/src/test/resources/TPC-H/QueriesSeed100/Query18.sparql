prefix xsd: <http://www.w3.org/2001/XMLSchema#>  
prefix rdfh: <http://lod2.eu/schemas/rdfh#> 
prefix qb: <http://purl.org/linked-data/cube#>

select
   ?c_name
   ?c_custkey
   ?o_orderkey
   ?o_orderdate
   ?o_ordertotalprice
   (sum((?l_linequantity)) as ?l_quantity)
where {
    ?li 
        rdfh:l_linequantity ?l_linequantity ;
        rdfh:l_has_order ?ord .
    ?ord rdfh:o_orderkey ?o_orderkey ;
         rdfh:o_orderdate ?o_orderdate ;
         rdfh:o_ordertotalprice ?o_ordertotalprice ;
         rdfh:o_has_customer ?cust .
    ?cust rdfh:c_custkey ?c_custkey ;
          rdfh:c_name ?c_name .  
    { select 
         ?ord 
         (sum ((?l2_linequantity)) as ?sum_q)
       where {
           ?li2 
                rdfh:l_linequantity ?l2_linequantity ;
                rdfh:l_has_order ?ord .
       }
       group by ?ord 
    } .
    filter ((?sum_q) > (312))
}
group by
   ?c_name
   ?c_custkey
   ?o_orderkey
   ?o_orderdate
   ?o_ordertotalprice
order by
  desc (?o_ordertotalprice)
  ?o_orderdate
limit 100
